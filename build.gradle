import org.apache.tools.ant.filters.ReplaceTokens

/**
 * HOW TO BUILD
 * 
 * To build this web application, you must build the frontend and the backend.
 *
 * FRONTEND BUILD:
 * Nodejs and npm must be globally installed (windows: must be on PATH).
 * Run 'gradle gulp_build' to build frontend (javascript and sass).
 * 
 * BACKEND BUILD & RUN:
 * See 'https://github.com/gretty-gradle-plugin/gretty' for detailed instructions.
 * User Gradle task 'appRun' to start embedded tomcat and deploy webapp.
 * 
 * CONFIGURATION:
 * Default configuration file can be found at 'src/main/resources/default.properties'.
 * Use configuration file at 'config/config.properties' to override defaults.
 * 
 * DEBUG:
 * Use Gradle task 'appRunDebug' to cause the webapp container to wait for debugger on port 5005.
 */


buildscript {
    repositories {
        // gradle node/gulp plugin
        maven {
	    	url "https://plugins.gradle.org/m2/"
	    }
	    // gretty, cargo
	    jcenter()
    }
    dependencies {
    	classpath 'org.gretty:gretty:3.0.1'
        classpath "com.github.node-gradle:gradle-node-plugin:1.3.0"
        classpath 'com.bmuschko:gradle-cargo-plugin:2.4'
    }
}

apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'org.gretty'
apply plugin: 'com.bmuschko.cargo'
apply plugin: 'com.github.node-gradle.node'
apply plugin: "com.github.node-gradle.gulp"

wrapper.gradleVersion = '6.1.1'

class Profile {
	String name
	
	String type
	String warName
	String workspaceLocation
	
	Profile(String name) {
	    this.name = name
	}
}
class ProfilesPlugin implements Plugin<Project> {
    void apply(final Project project) {
        NamedDomainObjectContainer<Profile> profileContainer = project.container(Profile)
        project.extensions.add('profiles', profileContainer)
    }
}
apply plugin: ProfilesPlugin

// external profiles in profile.gradle override these profiles
profiles {
   	local {
   		type = "dev"
   		// warName = "foo.war" // change default war name
   		workspaceLocation = "$projectDir/gmm-workspace"
   	}
   	ext.active = local
}
if (file('profile.gradle').exists()) {
	apply from: 'profile.gradle'
}

group = 'de.gothic-reloaded-mod'
version = '0.6.8-SNAPSHOT'

description = "Gothic Mod Manager"

sourceCompatibility = 1.8
targetCompatibility = 1.8

tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

repositories {
	// all
    mavenCentral()
    // gretty
    jcenter()
}
dependencies {

	// Spring 5
	ext.springVersion = '5.2.2.RELEASE'
	ext.springSecVersion = '5.2.1.RELEASE'
	
	implementation group: "com.realityinteractive.imageio.tga", name: "undefined"
	
	implementation group: 'org.springframework', name: 'spring-context', version: springVersion
    implementation group: 'org.springframework', name: 'spring-core', version: springVersion
    implementation group: 'org.springframework', name: 'spring-context-support', version: springVersion
    implementation group: 'org.springframework', name: 'spring-webmvc', version: springVersion
    implementation group: 'org.springframework', name: 'spring-test', version: springVersion
    implementation group: 'org.springframework', name: 'spring-beans', version: springVersion
    implementation group: 'org.springframework', name: 'spring-messaging', version: springVersion
    implementation group: 'org.springframework', name: 'spring-websocket', version: springVersion
    implementation group: 'org.springframework.security', name: 'spring-security-core', version: springSecVersion
    implementation group: 'org.springframework.security', name: 'spring-security-config', version: springSecVersion
    implementation group: 'org.springframework.security', name: 'spring-security-web', version: springSecVersion
    implementation group: 'org.springframework.security', name: 'spring-security-taglibs', version: springSecVersion
    implementation group: 'org.springframework.security', name: 'spring-security-messaging', version: springSecVersion

	// Libs
    implementation group: 'com.google.jimfs', name: 'jimfs', version:'1.1'
    implementation group: 'org.tmatesoft.svnkit', name: 'svnkit', version:'1.9.3'
    /* implementation(group: 'com.sun.mail', name: 'javax.mail', version:'1.5.3') {
		exclude(module: 'activation')
    } */
    implementation group: 'com.google.guava', name: 'guava', version:'18.0'
    //implementation group: 'joda-time', name: 'joda-time', version:'2.8.1'
	implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version:'2.9.4'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version:'2.9.4'
    implementation group: 'org.apache.commons', name: 'commons-lang3', version:'3.3.2'
    implementation group: 'org.apache.commons', name: 'commons-collections4', version:'4.1'
    implementation group: 'commons-io', name: 'commons-io', version: '2.6'
    implementation group: 'com.thoughtworks.xstream', name: 'xstream', version:'1.4.8'
    implementation group: 'org.freemarker', name: 'freemarker', version:'2.3.27-incubating'
    implementation group: 'ch.qos.logback', name: 'logback-classic', version:'1.2.3'
    implementation group: 'commons-fileupload', name: 'commons-fileupload', version:'1.3.1'
    implementation group: 'org.imgscalr', name: 'imgscalr-lib', version:'4.2'
    
    // JavaEE
    implementation group: 'javax.servlet', name: 'jstl', version:'1.2'
    
    // Testing
	testImplementation group: 'junit', name: 'junit', version:'4.12'
    
    // JavaEE APIs
    providedCompile group: 'javax.servlet', name: 'javax.servlet-api', version:'4.0.0'
    providedCompile group: 'javax.servlet.jsp', name: 'javax.servlet.jsp-api', version:'2.3.1'
	providedCompile group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.2'
    
	// Change gretty tomcat version
	def tomcatVersion = '9.0.30'
    gretty(
    	"org.apache.tomcat.embed:tomcat-embed-core:${tomcatVersion}",
        "org.apache.tomcat.embed:tomcat-embed-el:${tomcatVersion}",
        "org.apache.tomcat.embed:tomcat-embed-jasper:${tomcatVersion}",
        "org.apache.tomcat.embed:tomcat-embed-websocket:${tomcatVersion}"
    )
    
}

// #####################
//  Backend Build
// #####################

task printInfo {
	println 'Build Profile: ' + project.profiles.ext.active.name
	println 'Build Version: ' + project.version
}

// context.xml Filtering
task copyMetaInf(type: Copy) {
	// up-to-date for same profile workspace path
	inputs.property('profile', project.profiles.ext.active.workspaceLocation)
	
	from ("$projectDir/src/main/webapp/META-INF-src") {
    	exclude 'context.xml'
    }
    from ("$projectDir/src/main/webapp/META-INF-src") {
    	include 'context.xml'
	    filter(ReplaceTokens, tokens: [
	    	workspaceLocation: project.profiles.ext.active.workspaceLocation
		])
    }
    filteringCharset = 'UTF-8'
	into "$projectDir/src/main/webapp/META-INF"
}

// Resource Filtering
processResources {
	dependsOn copyMetaInf
	// up-to-date for same version & profile
	inputs.property('version', project.version)
	inputs.property('profile', project.profiles.ext.active.type)
	
    with copySpec {
    	from "$projectDir/src/main/resources/meta.properties"
    	expand(
			'gmm_version': project.version,
			'gmm_name': project.description,
			'gmm_profile_type': project.profiles.ext.active.type
		)
    }
}

war {
	exclude '**/META-INF-src'
	def profile = project.profiles.ext.active;
	if (profile.hasProperty('warName') && profile.warName) {
		archiveFileName = profile.warName
	}
}


// Embedded Tomcat/Jetty Configuration
gretty {
	httpPort = 8080
	contextPath = '/GMM'
	servletContainer = "tomcat9"
	//serverConfigFile = "tomcat.xml" // Connectors for HTTP2 can be specified in this file
	recompileOnSourceChange = false // call javaCompile to reload or auto reload
	enableNaming = true // enable JNDI in context.xml for embedded tomcat
	
	// gretty container log (has nothing to to with GMM's log)
	logDir = "$projectDir/build"
	logFileName = "gretty"
}

tasks.whenTaskAdded { task ->
		// https://github.com/gretty-gradle-plugin/gretty/issues/117
		if (task.name == 'prepareInplaceWebAppFolder') {
			task.dependsOn(processResources)
		}
		// disallow non-local profiles for appRun
		if (task.name == 'appRun') {
		task.doFirst {
			def profile = project.profiles.ext.active.name
			if (profile != 'local') {
				throw new InvalidUserDataException(
					"Expected profile to be set to 'local' for jetty task, but was '" + profile + "'!")
			}
		}
	}
}

cargo {
    containerId = 'tomcat9x'
    port = 8080
    deployable {
        context = '/GMM'
    }
    remote {
        hostname = 'localhost'
        username = 'tomcat'
        password = 'tomcat'
    }
}


// #####################
//  Fontend Build
// #####################

// installGulp is unnecessary since it should be installed by npm

gulp_three.dependsOn 'npmInstall'
gulp_build_js.dependsOn 'npmInstall'
gulp_build_sass.dependsOn 'npmInstall'

gulp_build.dependsOn 'npmInstall'
gulp_build.dependsOn 'gulp_three'

node {
	download = false
	nodeModulesDir = file("$projectDir/src/main/frontend")
}
gulp {
	workDir = file("$projectDir/src/main/frontend")
}